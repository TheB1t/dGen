program             = _{ SOI ~ (statement ~ ";")* ~ EOI }

statement           = _{
    var_decl
    | assignment
    | expr
}

var_decl            =  { type ~ identifier ~ ("=" ~ expr)? }
assignment          =  { identifier ~ "=" ~ expr }
type                = _{ tnum | tstr | tbool | tvoid | identifier }
tnum                =  { "number" }
tstr                =  { "string" }
tbool               =  { "boolean" }
tvoid               =  { "void" }

identifier          = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

expr                =  { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }
infix               = _{ add | sub | mul | div | mmod | and | or | eq | neq | gt | lt | gte | lte }
prefix              = _{ inc | dec | neg | not }
postfix             = _{ inc | dec }
add                 =  { "+" }
sub                 =  { "-" }
mul                 =  { "*" }
div                 =  { "/" }
mmod                =  { "%" }
and                 =  { "&&" }
or                  =  { "||" }
not                 =  { "!" }
eq                  =  { "==" }
neq                 =  { "!=" }
gt                  =  { ">" }
lt                  =  { "<" }
gte                 =  { ">=" }
lte                 =  { "<=" }
neg                 =  { "-" }
inc                 =  { "++" }
dec                 =  { "--" }
primary             = _{
    number
    | boolean
    | identifier
    | string
    | "(" ~ expr ~ ")"
}

number              = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
string              =  { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
btrue               =  { "true" }
bfalse              =  { "false" }
boolean             = _{ btrue | bfalse }

WHITESPACE          = _{ " " | "\t" | NEWLINE }
COMMENT             = _{ "//" ~ (!NEWLINE ~ ANY)* }
