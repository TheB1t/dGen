program             = _{ SOI ~ (stmt ~ ";")* ~ EOI }

stmt                = _{
    if_stmt
    | while_stmt
    | for_stmt
    | break_stmt
    | continue_stmt
    | return_stmt
    | func_decl
    | func_def
    | var_decl
    | assignment
    | array_assignment
    | expr
}

compound_stmt       =  { "{"  ~ (stmt ~ ";")*? ~ "}" }
expr_list           = _{ expr ~ ("," ~ expr)* }
param_list          =  { param ~ ("," ~ param)* }
type_list           =  { type ~ ("," ~ type)* }

param               =  { type ~ identifier }
decl                = _{ type ~ identifier }
func_def            =  { decl ~ "(" ~ param_list? ~ ")" ~ compound_stmt }
func_decl           =  { decl ~ "(" ~ type_list? ~ ")" }
var_decl            =  { type ~ identifier ~ ("=" ~ expr)? }
array_init          =  { "[" ~ expr_list? ~ "]" }

if_stmt             =  { "if" ~ "(" ~ expr ~ ")" ~ compound_stmt ~ ("else" ~ compound_stmt)? }
while_stmt          =  { "while" ~ "(" ~ expr ~ ")" ~ compound_stmt }
for_stmt            =  { "for" ~ "(" ~ var_decl ~ ";" ~ expr ~ ";" ~ assignment ~ ")" ~ compound_stmt }

break_stmt          =  { "break" }
continue_stmt       =  { "continue" }
return_stmt         =  { "return" ~ expr? }

func_call           =  { identifier ~ "(" ~ expr_list? ~ ")" }
assignment          =  { identifier ~ "=" ~ expr }
array_assignment    =  { array_access ~ "=" ~ expr }

identifier          = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
array_access        =  { identifier ~ "[" ~ expr ~ "]" }

expr                =  { prefix* ~ primary ~ postfix* ~ (infix ~ prefix* ~ primary ~ postfix* )* }
infix               = _{ add | sub | mul | div | mmod | and | or | eq | neq | gte | lte | gt | lt }
prefix              = _{ inc | dec | neg | not }
postfix             = _{ inc | dec }
add                 =  { "+" }
sub                 =  { "-" }
mul                 =  { "*" }
div                 =  { "/" }
mmod                =  { "%" }
and                 =  { "&&" }
or                  =  { "||" }
not                 =  { "!" }
eq                  =  { "==" }
neq                 =  { "!=" }
gt                  =  { ">" }
lt                  =  { "<" }
gte                 =  { ">=" }
lte                 =  { "<=" }
neg                 =  { "-" }
inc                 =  { "++" }
dec                 =  { "--" }
primary             = _{
    func_call
    | array_access
    | array_init
    | number
    | boolean
    | identifier
    | string
    | "(" ~ expr ~ ")"
}

number              = @{ (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ | ASCII_DIGIT) }
string              =  { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

btrue               =  { "true" }
bfalse              =  { "false" }
boolean             = _{ btrue | bfalse }

tarr                =  { type_base ~ "[]" }
tnum                =  { "number" }
tstr                =  { "string" }
tbool               =  { "boolean" }
tvoid               =  { "void" }
tobj                =  { "object" }
type_base           = _{ tnum | tstr | tbool | tvoid | tobj | identifier }
type                =  { tarr | type_base }

WHITESPACE          = _{ " " | "\t" | "\r" | NEWLINE }
COMMENT             = _{ "//" ~ (!NEWLINE ~ ANY)* }
